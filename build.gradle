import com.github.jengelman.gradle.plugins.shadow.transformers.AppendingTransformer

plugins {
	id 'groovy' // groovy support
	id 'java' // java support
	id 'scala' // scala support
	id 'com.diffplug.spotless' version '5.12.5'//code format
	id "com.github.johnrengelman.shadow" version "7.0.0" // fat jar
	id 'application'
}

application {
	mainClassName = 'info.coverified.tagging.main.Main'
}


ext {

	scalaVersion = '2.13'
	scalaBinaryVersion = '2.13.6'
	javaVersion = JavaVersion.VERSION_1_8

	slf4jVersion = '1.7.26'
	sentryVersion = '5.0.1'

	scriptsLocation = 'gradle' + File.separator + 'scripts' + File.separator //location of script plugins
}

apply from: scriptsLocation + 'spotless.gradle'
apply from: scriptsLocation + 'prepareEnv.gradle'
apply from: scriptsLocation + 'deploy.gradle'

group = 'info.coverified'
version = '0.1-SNAPSHOT'
description = 'Tagging Service'
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

configurations {
	scalaCompilerPlugin
}

repositories {
	mavenCentral() //searches in bintray's repository 'jCenter', which contains Maven Central
	maven { url 'https://jitpack.io' }
}

dependencies {

	// coverified dependencies
	implementation 'com.github.coverified:graphQLConnector:0b3824a'

	// CORE akka //
	implementation platform("com.typesafe.akka:akka-bom_${scalaVersion}:2.6.15")
	implementation "com.typesafe.akka:akka-actor-typed_${scalaVersion}"

	implementation "com.typesafe.akka:akka-stream_${scalaVersion}"
	testImplementation "com.typesafe.akka:akka-actor-testkit-typed_${scalaVersion}"

	// cmd args parser //
	implementation "com.github.scopt:scopt_${scalaVersion}:4.0.0"

	// http request
	implementation "org.scalaj:scalaj-http_${scalaVersion}:2.4.2"

	// https://mvnrepository.com/artifact/io.circe/circe-generic
	implementation group: 'io.circe', name: 'circe-generic_2.13', version: '0.13.0'

	// https://mvnrepository.com/artifact/net.liftweb/lift-json
	implementation group: 'net.liftweb', name: 'lift-json_2.13', version: '3.4.3'

	// https://mvnrepository.com/artifact/net.ruippeixotog/scala-scraper
	implementation group: 'net.ruippeixotog', name: 'scala-scraper_2.13', version: '2.2.0'

	// https://mvnrepository.com/artifact/com.github.ghostdogpr/caliban
	implementation group: 'com.github.ghostdogpr', name: "caliban_${scalaVersion}", version: '0.9.5'

	// https://mvnrepository.com/artifact/com.github.ghostdogpr/caliban-client
	implementation group: 'com.github.ghostdogpr', name: "caliban-client_${scalaVersion}", version: '0.9.5'

	// https://mvnrepository.com/artifact/com.github.ghostdogpr/caliban-tools
	implementation group: 'com.github.ghostdogpr', name: "caliban-tools_${scalaVersion}", version: '0.9.5'

	// logging
	implementation "org.slf4j:slf4j-api:${slf4jVersion}"  // slf4j wrapper
	implementation 'com.lmax:disruptor:3.4.2' // async logging
	implementation 'org.apache.logging.log4j:log4j-api:2.14.0' // log4j
	implementation 'org.apache.logging.log4j:log4j-core:2.14.0' // log4j
	implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.14.0' // log4j -> slf4j

	implementation "com.typesafe.scala-logging:scala-logging_${scalaVersion}:3.9.2" // akka scala logging
	implementation 'com.typesafe.scala-logging:scala-logging-slf4j_2.11:2.1.2'// scala logging
	implementation "org.slf4j:log4j-over-slf4j:${slf4jVersion}" // slf4j -> log4j

	// CORE Scala //
	implementation "org.scala-lang:scala-library:${scalaBinaryVersion}"
}

shadowJar {
	transform(AppendingTransformer) {
		resource = 'reference.conf'
	}
	with jar
}
